plugins {
    id 'groovy'
    id 'maven-publish'
    id 'signing'
}

group = 'com.kazurayam'
version = '0.4.0-SNAPSHOT'

def defaultEncoding = 'UTF-8'
tasks.withType(AbstractCompile).each {it.options.encoding = defaultEncoding }
tasks.withType(GroovyCompile).each {it.groovyOptions.encoding = defaultEncoding}

ext {
    AUTO_IMPORTED_JAR_PREFIX = 'AUTOIMPORTED_'
    webdriverfactoryVersion = '0.1.1-SNAPSHOT'
    groovyVersion = '2.4.20'
    junit4ksVersion = '1.6.1'
    isReleaseVersion = ! version.endsWith("SNAPSHOT")
}

repositories {
    mavenCentral()
    mavenLocal()
}

configurations {
    myconf
    generateDocs
}

dependencies {
    implementation fileTree(dir: "/Applications/Katalon Studio.app/Contents/Eclipse/configuration/resources/lib", include: ['*.jar'])
    implementation fileTree(dir: "/Applications/Katalon Studio.app/Contents/Eclipse/plugins", include: ['*.jar'])
    implementation "org.codehaus.groovy:groovy-all:${groovyVersion}"
    implementation group: 'com.kazurayam', name: 'webdriverfactory',
            version: "${webdriverfactoryVersion}"
    myconf group: 'com.kazurayam', name: 'webdriverfactory',
            version: "${webdriverfactoryVersion}"
    myconf group: 'com.kazurayam', name: 'junit4ks',
            version: "${junit4ksVersion}"
    generateDocs "org.codehaus.groovy:groovy-all:${groovyVersion}"
}

sourceSets {
    main {
        groovy {
            srcDirs = ['Keywords', 'Libs']
            srcDir 'Libs'
            excludes = ['CustomKeywords.groovy', "Temp*.groovy"]
        }
    }
    test {
        groovy {
            srcDirs = ['Include/scripts/groovy']
        }
    }
}
task drivers {
	doFirst {
	    delete fileTree("Drivers") {
	        include("**/" + AUTO_IMPORTED_JAR_PREFIX + "*")
	    }
	}
	doLast {
	    copy { copySpec ->
	        copySpec
	            .from(project.getConfigurations().getByName("myconf"))
	            .into("Drivers")
	            .include(
	                "**/webdriverfactory*.jar",
	                "**/junit4ks*.jar"
	                )
	            .rename({ s ->
	                AUTO_IMPORTED_JAR_PREFIX + s
	                })
	    }
	}
}

task groovydoc(type: Groovydoc, overwrite:true) {
    source = sourceSets.main.groovy
    groovyClasspath = project.configurations.generateDocs

    include 'com/kazurayam/webdriverfactory4ks/*'

    exclude '**/*Test.groovy'
}

task publishGroovydoc(type: Copy) {
    from 'build/docs/groovydoc'
    into 'docs/api'
}

groovydoc.finalizedBy publishGroovydoc

task groovydocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from groovydoc
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}
